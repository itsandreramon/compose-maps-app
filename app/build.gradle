plugins {
    id 'com.android.application'
    id 'dagger.hilt.android.plugin'
    id 'com.google.secrets_gradle_plugin' version '0.6'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

kapt {
    correctErrorTypes = true
    useBuildCache = true
}

secrets {
    propertiesFileName 'secrets.properties'
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdk buildConfig.compileSdk

    defaultConfig {
        minSdk buildConfig.minSdk
        targetSdk buildConfig.targetSdk

        versionName "1.0"
        versionCode 1

        applicationId "de.thb.rulona"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug

            versionNameSuffix "-dev"
            applicationIdSuffix ".debug"
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    signingConfigs {
        debug {
            keyAlias "debug"
            keyPassword keystoreProperties["debugKeyPassword"]
            storePassword keystoreProperties["debugStorePassword"]
            storeFile rootProject.file("release/debug.keystore.jks")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion libs.versions.androidx.compose.lib.get()
    }
}

dependencies {
    implementation project(':core')
    implementation project(':ui')

    implementation libs.androidx.activity.compose
    implementation libs.androidx.navigation.compose

    implementation libs.androidx.compose.ui.ui
    implementation libs.androidx.compose.ui.tooling
    implementation libs.androidx.compose.material.material

    kapt libs.google.hilt.compiler
    implementation libs.google.hilt.android

    implementation libs.google.play.location
    implementation libs.google.play.maps

    implementation libs.airbnb.mavericks.compose
    implementation libs.google.accompanist.insets

    debugImplementation libs.square.leakCanary
}